/**
Handles achievements and their messages
The fourth row of text (rows y = [44 - 54]) is dedicated to achievement messages and is entirely cleared upon Achievements.clearMessage()
The top right corner displays the number of achievements left.

When the last achievement is complete, this enters endscreen (won the game)

0: gravity flip while in air
1: left/right arrow to change cube jump velocity
2: crash into a spike cluster
3: press ? key
4: 1000 points
5: 50 jumps (not including repeated jumps by holding down a jump key)
6: set the game speed with digit key
7: perform at least one gravity flip
8: land on the head of a spaceship (springboard)

*/

class Achievements {
    static int numAchievements, achievementsLeft;
    static Array achievements;
    static Array messages;
    
    static int numJumps;
    static boolean gameEnded;
    
    function void init() {
        
        let numAchievements = 9;
        let achievementsLeft = numAchievements;
        let achievements = Array.new(numAchievements);
        let messages = Array.new(numAchievements);
        let numJumps = 0;
        let gameEnded = false;
        
        let messages[0] = "Gravity glitch! Cubes can fly?";
        let messages[1] = "So, you've found the jump boost, eh?";
        let messages[2] = "Ouch! Don't impale the cube again!";
        let messages[3] = "Now you know the secrets; good luck!";
        let messages[4] = "1000 points. Geometry master.";
        let messages[5] = "Nice rhythm with those jumps.";
        let messages[6] = "Speed boost activated.";
        let messages[7] = "Gravity flip discovered!";
        let messages[8] = "You bounced off a spaceship!";
        
        do Achievements.writeAchievementsLeft();
        return;
    }
    
    function void writeAchievementsLeft() {
        do Output.moveCursor(0, 38);
        do Output.printString("Achievements remaining:");
        do Output.moveCursor(0, 62);
        do Output.printInt(achievementsLeft);
        return;
    }
    
    function boolean complete(int achievement) {
    
        // first check if it is the end of the game
        // end the game if score > 1500 (achievement -1) and if there are no achievementsLeft and if we have not already ended the game
        if ((achievement = -1) & (achievementsLeft = 0) & (~gameEnded)) {
            do Sys.wait(1000);
            do Achievements.clearMessage();
            do Endscreen.win();
            let gameEnded = true;
        }
        
        // corner cases
        if (achievement < 0) {return false;}
        if (~(achievement < numAchievements)) {do Sys.error(2);}
        if (achievements[achievement]) {return false;} // already complete
        
        // specific achievement cases
        if (achievement = 3) {
            do Achievements.displaySecrets();
        }
        else {
            if (achievement = 5) {
                let numJumps = numJumps + 1;
                if (~(numJumps = 50)) {return false;} // did not just do the fiftieth jump
            }
            else {
                if (achievement = 2) {
                    do Achievements.clearLower(); // wipe the spikes/ground
                }
            }
        }
        
        // completed achievement
        let achievements[achievement] = true;
        
        do Achievements.clearMessage();
        let achievementsLeft = achievementsLeft - 1;
        do Output.moveCursor(0, 62);
        do Output.printInt(achievementsLeft);
        
        do Output.moveCursor(4, 20);
        do Output.printString(messages[achievement]);
        
        return true;
    }
    
    function void clearMessage() {
        var int addr;
        let addr = 17792; //16384 + 44*32
        
        while (addr < 18144) { // < 16384 + 55*32
            do Memory.poke(addr, 0);
            let addr = addr + 1;
        }   
        return;
    }
    
    function void clearLower() {
        var int addr;
        let addr = 17792; //16384 + 44*32
        
        while (addr < 24577) { // < 16384 + 256*32 + 1
            do Memory.poke(addr, 0);
            let addr = addr + 1;
        }   
        return;
    }

    function void displaySecrets() {
        var int i;
        var String completed;
        let completed = "  <completed>";
        
        let i = 0;
        while (i < numAchievements) {
            do Output.moveCursor(6 + i, 10);
            do Output.printString(messages[i]);
            if (achievements[i]) {do Output.printString(completed);}
            let i = i + 1;
        }
        do completed.dispose();
        let completed = "Press any key to return to the game";
        do Output.moveCursor(7 + i, 10);
        do Output.printString(completed);
        do completed.dispose();
        
        // wait for key release
        while (~(Keyboard.keyPressed() = 0)) {}
        
        // wait for press key
        while ((Keyboard.keyPressed() = 0)) {}
        do Achievements.clearLower();
        return;
    }
}
