class Endscreen {
    static String winPrompt;
    
    /** 
     * Pantalla de choque: devuelve true si el jugador quiere jugar de nuevo.
     */
    function boolean crash() {
        var boolean playAgain;
        var int key;
        var String str;
        var boolean play;
        let play = true;

        let playAgain = false;

        // Mostrar mensaje
        let str = "Press space to play again, or press q to quit"; 
        do Output.moveCursor(10, 10);
        do Output.printString(str);
        do str.dispose();
        
        // Esperar hasta que no haya teclas presionadas
        while (~(Keyboard.keyPressed() = 0)) {}
        
        // Esperar por espacio o q
        while (play) {
            let key = Keyboard.keyPressed();

            // espacio, flecha arriba, flecha abajo → jugar de nuevo
            if ((key = 131) | (key = 133)) {
                let playAgain = true;
                do Sys.wait(10);
                let play = false;
            }

            // q o Q → salir
            if ((key = 81) | (key = 113)) {
                let playAgain = false;
                do Sys.wait(10);
                let play = false;
            }
        }
        
        return playAgain;
    }
    
    /**
     * Pantalla de victoria: se llama cuando todos los logros están completos.
     * Muestra "YOU WIN" y un cubo rebotando hasta que el usuario continúe o salga.
     */
    function void win() {
        var boolean stop;
        var int key;
        var Cube cube;
        var String str;

        let stop = false;
        let cube = Cube.new(7300); // empezar en el suelo

        // Limpiar área y mostrar instrucciones
        let str = "You completed all achievements!";
        do Output.moveCursor(8, 10);
        do Output.printString(str);
        do str.dispose();

        let str = "Press space to continue or q to quit";
        do Output.moveCursor(10, 10);
        do Output.printString(str);
        do str.dispose();
        
        // Bucle principal de victoria
        while (~stop) {

            // Actualizar el cubo (física y dibujo)
            do cube.update();

            // Dibujar texto "YOU WIN"
            do Graphics.writeYouWin(2826);
            
            let key = Keyboard.keyPressed();

            // espacio, arriba, abajo, q, o Q detiene la pantalla de victoria
            if ((key = 32) | (key = 131) | (key = 133) | (key = 81) | (key = 113)) {
                let stop = true;
            }
            
            do Sys.wait(10);
        }
        
        // Si se presionó q → salir del juego completamente
        if ((key = 81) | (key = 113)) {
            do cube.dispose();
            do Sys.exit();
            return;
        }
        
        // De lo contrario, volver al bucle de juego
        do cube.dispose();
        do Achievements.clearLower();
        return;
    }
}